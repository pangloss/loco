(ns loco.constraints.int-value-precede-chain
  (:require
   [clojure.core.match :refer [match]]
   [clojure.spec.alpha :as s]
   [clojure.walk :as walk]
   [loco.constraints.utils :refer :all :as utils]
   )
  (:import
   [org.chocosolver.solver.variables SetVar IntVar BoolVar Task]))

(def ^:private constraint-name 'int-value-precede-chain)

(s/def ::compile-spec
  (s/cat :constraint #{constraint-name}
         :args       (s/spec
                      (s/tuple
                       ::utils/coll-coerce-intvar?
                       (s/coll-of int?)))))

(defn- compiler [model vars-index statement]
  (let [var-subed-statement (->> statement (walk/prewalk-replace vars-index))
        coerce-var (utils/coerce-var model)]
    (match (->> var-subed-statement (s/conform ::compile-spec))
           {:args [variables values]}
           (.intValuePrecedeChain
            model
            (->> variables (map coerce-var) (into-array IntVar))
            (int-array values))

           ::s/invalid
           (report-spec-error constraint-name ::compile-spec var-subed-statement))))

(defloco $int-value-precede-chain
  "CHOCO:
intValuePrecedeChain(IntVar[] X, int[] V)
Ensure that, for each pair of V[k] and V[l] of values in V, such that
k < l, if there exists j such that X[j] = V[l], then, there must exist
i < j such that X[i] = V[k].  i < j such that X[i] = V[k].

GCCAT:
Arguments
    ðš…ð™°ð™»ðš„ð™´ðš‚	ðšŒðš˜ðš•ðš•ðšŽðšŒðšðš’ðš˜ðš—(ðšŸðšŠðš›-ðš’ðš—ðš)
    ðš…ð™°ðšð™¸ð™°ð™±ð™»ð™´ðš‚	ðšŒðš˜ðš•ðš•ðšŽðšŒðšðš’ðš˜ðš—(ðšŸðšŠðš›-ðšðšŸðšŠðš›)

Restrictions
    ðš›ðšŽðššðšžðš’ðš›ðšŽðš(ðš…ð™°ð™»ðš„ð™´ðš‚,ðšŸðšŠðš›)
    ðšðš’ðšœðšðš’ðš—ðšŒðš(ðš…ð™°ð™»ðš„ð™´ðš‚,ðšŸðšŠðš›)
    ðš›ðšŽðššðšžðš’ðš›ðšŽðš(ðš…ð™°ðšð™¸ð™°ð™±ð™»ð™´ðš‚,ðšŸðšŠðš›)

Purpose
    Assuming n denotes the number of items of the ðš…ð™°ð™»ðš„ð™´ðš‚ collection,
    the following condition holds for every iâˆˆ[1,n-1]: When it is
    defined, the first occurrence of the (i+1)th value of the ðš…ð™°ð™»ðš„ð™´ðš‚
    collection should be preceded by the first occurrence of the ith
    value of the ðš…ð™°ð™»ðš„ð™´ðš‚ collection.

Example
    (âŒ©4,0,6,1,0âŒª, âŒ©4,0,1âŒª)

    The ðš’ðš—ðš_ðšŸðšŠðš•ðšžðšŽ_ðš™ðš›ðšŽðšŒðšŽðšðšŽ_ðšŒðš‘ðšŠðš’ðš— constraint holds since within the sequence (4, 0, 6, 1, 0):
    - The first occurrence of value 4 occurs before the first occurrence of value 0.
    - The first occurrence of value 0 occurs before the first occurrence of value 1."
  {:choco ["intValuePrecedeChain(IntVar[] X, int[] V)"]
   :gccat ["http://sofdem.github.io/gccat/gccat/Cint_value_precede_chain.html"]}
  ([variables values]
   {:pre [(sequential? values) (distinct? values) (every? int? values) (sequential? variables)]}
   (constraint constraint-name
               [(vec variables) (vec values)]
               compiler)))
